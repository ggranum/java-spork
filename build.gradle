/*
 * This software is licensed under the MIT License.
 *
 * Copyright (c) 2015 Geoff M. Granum
 */

import biz.granum.gradle.BuildProps

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}

apply plugin: BuildProps;

buildProps.source("${project.rootDir}/gradle.local.properties");
buildProps.required("build.env");
buildProps.required(["project.version"])

buildProps.optional([])

subprojects {

    configurations.all({
        exclude(module: 'slf4j-simple')
        resolutionStrategy { ->
            // fail eagerly on version conflict (includes transitive dependencies)
            // e.g. multiple different versions of the same dependency (group and name are equal)
            failOnVersionConflict()

            // force certain versions of dependencies (including transitive)
            //  This appends new forced modules:
            force("org.hibernate:hibernate-validator:${project['versions.hibernateValidator']}",
                    "com.google.guava:guava:${project['versions.guava']}",
                    "commons-codec:commons-codec:${project['versions.commonsCodec']}",
                    "commons-lang:commons-lang3:${project['versions.commonsLang3']}",
                    "commons-io:commons-io:${project['versions.commonsIo']}",
                    "org.slf4j:slf4j-api:${project['versions.slf4j']}",
            )
            //  This would replace existing forced modules with new ones:
            //forcedModules = ['asm:asm-all:3.3.1']

            // cache dynamic versions for 10 minutes
            cacheDynamicVersionsFor 10 * 60, 'seconds'
            // don't cache changing modules at all
            cacheChangingModulesFor 0, 'seconds'
        }
    })

    repositories {
        mavenCentral()
//        maven {
//            url rootProject["yourRepo.url"]
//            credentials {
//                username = rootProject["yourRepo.username"]
//                password = rootProject["yourRepo.password"]
//            }
//        }
    }

    configurations {
        deployerJars // maven upload archive tasks
    }

    dependencies {
        deployerJars 'org.apache.maven.wagon:wagon-http:2.2' // maven upload archive tasks
    }


}

subprojects {
    if(project.hasProperty('test')) {
        test { ->
            // enable TestNG support (default is JUnit)
            useTestNG()
            workingDir "${project.rootDir}/"
        }
    }
}
